allprojects {
    project.ext.publishRepoName = "public-water-repository"
    project.ext.publishRepoUrl = "https://nexus.acsoftware.it/nexus/repository/maven-water/"
    project.ext.publishRepoUsername = System.getProperty("publishRepoUsername", "noUser")
    project.ext.publishRepoPassword = System.getProperty("publishRepoPassword", "noPassword")

    group 'it.water.core'
    version project.waterVersion
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {

            credentials {
                username project.publishRepoUsername
                password project.publishRepoPassword
            }

            name = project.publishRepoName
            url = project.publishRepoUrl
        }
    }

    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'jacoco'

    publishing {
        repositories {
            maven {

                credentials {
                    username project.publishRepoUsername
                    password project.publishRepoPassword
                }

                name = project.publishRepoName
                url = project.publishRepoUrl
            }
        }
    }

    test {
        finalizedBy jacocoTestReport
    }
}

task jacocoRootReport(type: JacocoReport) {
    dependsOn subprojects.test  // Assicura che tutti i test siano eseguiti prima del report

    // Specifica quali sottoprogetti includere
    additionalSourceDirs.setFrom(files(subprojects.sourceSets.main.allSource.srcDirs))
    sourceDirectories.setFrom(files(subprojects.sourceSets.main.allSource.srcDirs))
    classDirectories.setFrom(files(subprojects.sourceSets.main.output))

    // Aggiungi i file di esecuzione generati da ciascun progetto figlio
    executionData.setFrom(fileTree(project.rootDir.absolutePath).include("**/build/jacoco/test.exec"))

    reports {
        xml.required = true
        html.required = true
        csv.required = false
        xml.destination file("${buildDir}/reports/jacoco/jacocoRootReport.xml")
        html.destination file("${buildDir}/reports/jacoco/jacocoRootReport.html")
    }
}



